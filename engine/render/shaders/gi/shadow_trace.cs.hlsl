#include "rhi_bindings.hlsli"
// Screen-space sun shadow ray trace producing NRD's IN_PENUMBRA for the
// SIGMA_SHADOW denoiser. One ray per pixel toward the sun; the packed value
// carries the distance to the occluder so SIGMA can size the penumbra.
#include "NRD.hlsli"

[[vk::image_format("r16f")]] [[vk::binding(0, 0)]] RWTexture2D<float> penumbra_out : register(u0, space0);
[[vk::binding(1, 0)]] Texture2D<float> depth_map : register(t1, space0);
[[vk::binding(2, 0)]] RaytracingAccelerationStructure tlas : register(t2, space0);

struct PushData {
  column_major float4x4 inv_view_proj;  // unjittered
  float to_light_x;                     // direction toward the sun (normalized),
  float to_light_y;                     // kept as scalars to avoid float3 push
  float to_light_z;                     // constant alignment surprises
  float near_plane;
  float2 inv_size;
  float tan_angular_radius;             // tan(sun angular radius)
  float max_distance;                   // shadow ray length
  float2 jitter;                        // ndc projection jitter (screen = unjittered + jitter)
};
PUSH_CONSTANTS(PushData, push);

[numthreads(8, 8, 1)]
void main(uint3 id : SV_DispatchThreadID) {
  uint width, height;
  penumbra_out.GetDimensions(width, height);
  if (id.x >= width || id.y >= height) return;
  int3 p = int3(id.xy, 0);

  float depth = depth_map.Load(p);
  if (depth <= 0.0) {  // sky: fully lit
    penumbra_out[id.xy] = SIGMA_FrontEnd_PackPenumbra(NRD_FP16_MAX, push.tan_angular_radius);
    return;
  }

  float2 uv = (float2(id.xy) + 0.5) * push.inv_size;
  // The depth buffer is rendered with a jittered projection (screen = unjittered
  // + jitter), but inv_view_proj is unjittered. Undo the jitter on the pixel
  // center so the reconstructed world pos sits on the actual sampled surface
  // instead of wobbling ~slope*jitter each frame (that wobble made the 1-spp
  // sun shadow flicker, which SIGMA then bled through during camera motion).
  float2 ndc = uv * 2.0 - 1.0 - push.jitter;
  float4 world = mul(push.inv_view_proj, float4(ndc, depth, 1.0));
  float3 world_pos = world.xyz / world.w;

  float3 to_light = float3(push.to_light_x, push.to_light_y, push.to_light_z);
  // Self-intersection offset scaled with view depth: the reconstructed
  // world position's error (depth quantization + fp32) grows with distance,
  // and a fixed 2cm starts striping large flat triangles past ~40m. The
  // scaled offset keeps a constant projected size, so it stays invisible.
  float view_z = push.near_plane / max(depth, 1e-7);
  RayDesc ray;
  ray.Origin = world_pos + to_light * max(0.02, view_z * 0.004);
  ray.TMin = 0.001;
  ray.Direction = to_light;
  ray.TMax = push.max_distance;
  RayQuery<RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH | RAY_FLAG_FORCE_OPAQUE> rq;
  rq.TraceRayInline(tlas, RAY_FLAG_NONE, 0xff, ray);
  rq.Proceed();

  float distance_to_occluder =
      rq.CommittedStatus() == COMMITTED_TRIANGLE_HIT ? rq.CommittedRayT() : NRD_FP16_MAX;
  penumbra_out[id.xy] = SIGMA_FrontEnd_PackPenumbra(distance_to_occluder, push.tan_angular_radius);
}
